statemachine class GA_Storage extends SU_StorageItem {
  default tag = "GA_Storage";

  // The opponent for the next/ongoing match of gwent
  var opponent: CNewNPC;
  var bet_amount: int;

  var current_match: GA_GeneralDataCurrentMatch;
}

state Idle in GA_Storage {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);

    GALOG("GA_Storage - [Idle]");
  }
}

state Betting in GA_Storage {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);

    GALOG("GA_Storage - [Betting]");
    this.Betting_main();
  }

  entry function Betting_main() {
    if (!GA_isPlayerBusy()) {
      parent.bet_amount = 0;
      this.openHaggleWindow();
    }

    parent.GotoState('Idle');
  }

  function openHaggleWindow() {
    var haggle_module_dialog: GA_BountyModuleDialog;

    haggle_module_dialog = new GA_BountyModuleDialog in parent;
    haggle_module_dialog.openBettingWindow();
  }
}

state StartNoDialog in GA_Storage {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);

    GALOG("GA_Storage - [StartNoDialog]");
    this.StartNoDialog_main();
  }

  entry function StartNoDialog_main() {
    Sleep(2.5);
    parent.GotoState('Betting');
  }
}

state StartFromDialog in GA_Storage {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);

    GALOG("GA_Storage - [StartFromDialog]");
    this.StartFromDialog_main();
  }

  entry function StartFromDialog_main() {
    while (GA_isPlayerInScene()) {
      Sleep(0.2);
    }

    Sleep(0.2);
    parent.GotoState('Betting');
  }
}

state Playing in GA_Storage {
  event OnEnterState(previous_state_name: name) {
    super.OnEnterState(previous_state_name);

    GALOG("GA_Storage - [Playing]");
    this.Playing_main();
  }

  entry function Playing_main() {
    this.startMatch();
    parent.GotoState('Idle');
  }

  function startMatch() {
    var gwintManager:CR4GwintManager = theGame.GetGwintManager();
    gwintManager.testMatch = true;

    if (!GA_isPlayerBusy()) {
      thePlayer.OnGwintGameRequested('AGwentEmpireCustomDeck', GwintFaction_NothernKingdom);
    }
  }
}

function GA_getStorage(): GA_Storage {
  var data: GA_Storage;

  data = SU_getStorage().getItem("GA_Storage") as GA_Storage;

  // create the data if it's the first time:
  if (!data) {
    data = new GA_Storage in thePlayer;
    SU_getStorage().setItem(data);
  }

  return data;
}

/**
 * Unlike the regular getStorage function this one reset the storage when it
 * is called, great way to ensure the storage always uses a recent instance
 * of the class after an update.
 */
function GA_getCleanStorage(): GA_Storage {
  var data: GA_Storage;

  data = new GA_Storage in thePlayer;
  SU_getStorage().setItem(data);

  return data;
}

// return true in cases where the player is busy in a cutscene or in the boat.
// When a spawn should be cancelled.
function GA_isPlayerBusy(): bool {
  return thePlayer.IsInCombat()
      || thePlayer.IsUsingBoat()
      || thePlayer.IsSwimming()
      || GA_isPlayerInScene();
}

function GA_isPlayerInScene(): bool {
  return thePlayer.IsInNonGameplayCutscene()
      || thePlayer.IsInGameplayScene()
      || !thePlayer.IsActionAllowed(EIAB_DrawWeapon)
      || thePlayer.IsCiri()
      || theGame.IsDialogOrCutscenePlaying()
      || theGame.IsCurrentlyPlayingNonGameplayScene()
      || theGame.IsFading()
      || theGame.IsBlackscreen();
}